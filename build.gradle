apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'java'

apply from: "${rootDir}/gradle/object_layout.gradle"

group = 'com.github.ben-manes'

sourceCompatibility = JavaVersion.VERSION_1_8

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.6'
    classpath 'me.champeau.gradle:jmh-gradle-plugin:0.1.3'
  }
}

repositories {
  jcenter()
}

dependencies {
  def guava = '18.0'

  // TODO(ben): remove compile-time dependency
  compile "com.google.guava:guava:${guava}"

  // compile time (annotations only)
  compile 'com.google.code.findbugs:jsr305:3.0.0'

  // unit testing
  testCompile 'org.mockito:mockito-core:1.10.15'
  testCompile 'org.assertj:assertj-core:1.7.0'
  testCompile 'org.assertj:assertj-guava:1.3.0'
  testCompile 'com.jayway.awaitility:awaitility:1.6.3'
  testCompile "com.google.guava:guava-testlib:${guava}"
  testCompile 'org.testng:testng:6.8.8'

  // benchmarking
  jmh 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4'
  jmh 'net.openhft:koloboke-api-jdk8:0.6.5'
  jmh 'net.openhft:koloboke-impl-jdk8:0.6.5'
  
  // TODO(ben): Fix jmh to properly inform Eclipse of the library dependencies
  testCompile 'net.openhft:koloboke-api-jdk8:0.6.5'
  testCompile 'net.openhft:koloboke-impl-jdk8:0.6.5'
  testCompile 'org.openjdk.jmh:jmh-core:1.3.4'
}

test {
  useTestNG()
}

jmh {
  if (project.hasProperty('jmhIncludePattern')) {
    include = project.jmhIncludePattern
  }
  jvmArgs = '-XX:+UseG1GC -XX:+AlwaysPreTouch'
  warmupIterations = 10
  failOnError = true
  iterations = 10
  forceGC = true
  fork = 2
}
