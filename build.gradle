apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.kt3k.coveralls'

buildscript {
  apply from: "${rootDir}/gradle/dependencies.gradle"

  repositories {
    jcenter()
  }

  dependencies {
    classpath gradle_plugins.bundle
    classpath gradle_plugins.coveralls
    classpath gradle_plugins.jmh
    classpath gradle_plugins.nexus
    classpath gradle_plugins.semantic_versioning
    classpath gradle_plugins.stats
    classpath gradle_plugins.versions
  }
}

subprojects { proj ->
  apply from: "${rootDir}/gradle/jmh.gradle"
  apply from: "${rootDir}/gradle/publish.gradle"
  apply from: "${rootDir}/gradle/code_quality.gradle"
  apply from: "${rootDir}/gradle/dependencies.gradle"
  apply from: "${rootDir}/gradle/object_layout.gradle"

  apply plugin: 'com.github.ethankhall.semantic-versioning'
  apply plugin: 'org.dm.bundle'
  apply plugin: 'java'

  sourceCompatibility = JavaVersion.VERSION_1_8
  compileJava.options.incremental = true

  group = 'com.github.ben-manes.caffeine'
  version.with {
    major = 1 // incompatible API changes
    minor = 0 // backwards-compatible additions
    patch = 0 // backwards-compatible bug fixes
    releaseBuild = rootProject.hasProperty('release')
  }

  repositories {
    jcenter()
  }
  
  rootProject.tasks.coveralls {
    dependsOn proj.tasks.'check'
    onlyIf { System.env.'CI' }
  }
}
