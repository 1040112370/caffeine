apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'

buildscript {
  apply from: "${rootDir}/gradle/dependencies.gradle"

  repositories {
    jcenter()
  }

  dependencies {
    classpath gradle_plugins.bundle
    classpath gradle_plugins.coveralls
    classpath gradle_plugins.error_prone
    classpath gradle_plugins.jmh
    classpath gradle_plugins.nexus
    classpath gradle_plugins.semantic_versioning
    classpath gradle_plugins.stats
    classpath gradle_plugins.versions
  }
}

task testReport(type: TestReport, group: 'Build') {
  description = 'Generates an aggregate test report'
  destinationDir = file("${buildDir}/reports/allTests")
}

subprojects { proj ->
  if (proj.name == 'tracing') {
    return
  }

  apply plugin: 'com.github.ethankhall.semantic-versioning'
  apply plugin: 'org.dm.bundle'
  apply plugin: 'errorprone'
  apply plugin: 'java'

  apply from: "${rootDir}/gradle/publish.gradle"
  apply from: "${rootDir}/gradle/code_quality.gradle"
  apply from: "${rootDir}/gradle/dependencies.gradle"
  apply from: "${rootDir}/gradle/object_layout.gradle"

  sourceCompatibility = JavaVersion.VERSION_1_8
  compileJava.options.incremental = true

  group = 'com.github.ben-manes.caffeine'
  version.with {
    major = 1 // incompatible API changes
    minor = 0 // backwards-compatible additions
    patch = 0 // backwards-compatible bug fixes
    releaseBuild = rootProject.hasProperty('release')
  }
  archivesBaseName = path[1..-1].replaceAll(':', '-').toLowerCase()

  repositories {
    jcenter()
  }
  
  configurations {
    testArtifacts
  }
  
  tasks.withType(Test) {
    if (!it.name.startsWith('slow')) {
      rootProject.testReport.reportOn it
    }
  }
  
  task testJar(type: Jar, group: "Build") {
    description = "Assembles a jar archive containing the test classes."
    baseName = "${archivesBaseName}-test"
    from sourceSets.test.output
  }
  
  artifacts {
    testArtifacts testJar
  }
}

repositories {
  jcenter()
}

// Only report code coverage for projects that are distrubuted
def publishedProjects = subprojects.findAll {
  !it.path.startsWith(':simulator') && (it.path != ':tracing') && !it.path.startsWith(':jcache')
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
  description = 'Generates an aggregate report from all subprojects'
  dependsOn(publishedProjects.test)

  additionalSourceDirs = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  sourceDirectories = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  classDirectories = files(publishedProjects.sourceSets.main.output)
  executionData = files(publishedProjects.jacocoTestReport.executionData)

  reports {
    html.enabled = true
    xml.enabled = true
  }

  doFirst {
    executionData = files(executionData.findAll { it.exists() })
  }
}

coveralls {
  sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
  jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
  group = 'Coverage reports'
  description = 'Uploads the aggregated coverage report to Coveralls'

  dependsOn jacocoRootReport
  onlyIf { System.env.'CI' }
}
