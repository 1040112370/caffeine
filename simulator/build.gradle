/** Cache simulator using tracing data and a family of eviction policy options. */
apply plugin:'application'

mainClassName = 'com.github.benmanes.caffeine.cache.simulator.Simulator'

dependencies {
  compile project(':caffeine')

  compile libraries.akka
  compile libraries.joor
  compile libraries.guava
  compile libraries.stream
  compile libraries.flip_tables
  compile benchmark_libraries.tcache
  compile libraries.univocity_parsers
  compile benchmark_libraries.cache2k
  compile benchmark_libraries.ehcache2
  compile benchmark_libraries.ehcache3
  compile benchmark_libraries.slf4j_nop
  compile benchmark_libraries.infinispan

  testCompile test_libraries.jimfs
  testCompile test_libraries.testng
  testCompile test_libraries.awaitility
}

test {
  useTestNG()
}

bundle {
  instruction 'Import-Package', [
    'com.github.benmanes.caffeine.cache',
  ].join(';')
  instruction 'Export-Package', [
    'com.github.benmanes.caffeine.simulator',
    'com.github.benmanes.caffeine.simulator.admission',
    'com.github.benmanes.caffeine.simulator.generator',
    'com.github.benmanes.caffeine.simulator.parser',
    'com.github.benmanes.caffeine.simulator.policy',
    'com.github.benmanes.caffeine.simulator.report',
  ].join(';')
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
}

run {
  systemProperties System.properties.findAll { 
    it.getKey().startsWith('akka') || it.getKey().startsWith('caffeine')
  }
}
