/** Cache tracing libraries as separate API and implementation dependencies. */
apply plugin: 'eclipse'

configurations {
  api.extendsFrom compile
}

sourceSets {
  api {
    java.srcDir 'src/api/java'
    resources.srcDir 'src/api/resources'
  }
}

dependencies {
  apiCompile libraries.jsr305

  compile sourceSets.api.output
  compile project(':caffeine')
  compile libraries.disruptor

  testCompile libraries.guava
  testCompile test_libraries.testng
  testCompile test_libraries.mockito
  testCompile test_libraries.awaitility
}

plugins.withType(EclipsePlugin) {
  project.eclipse.classpath.plusConfigurations += [ configurations.api ]
}

test {
  useTestNG()
}

task apiJar(type: Jar, group: "Build") {
  description = 'Assembles a jar archive containing the api classes.'
  baseName = "${project.name}-api"
  from sourceSets.api.output
}

artifacts {
  api apiJar
}
